buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath("com.github.jengelman.gradle.plugins:shadow:4.0.4")
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "redisvelocity-plugin"
version = version

configurations {
    provided {
        compile.extendsFrom(provided)
    }

    build.dependsOn("uploadArchives")
    compileJava.dependsOn(":api:build")
    jar.enabled = false
    jar.dependsOn("shadowJar")
}

repositories {
    jcenter()
    maven {
        name = "velocity"
        url = "https://repo.velocitypowered.com/snapshots"
    }
}

dependencies {
    shadow(project(path: ":api"))
    annotationProcessor("com.velocitypowered:velocity-api:1.0.0-SNAPSHOT")
    provided("com.velocitypowered:velocity-api:1.0.0-SNAPSHOT")
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

shadowJar {
    classifier = null
    configurations = [project.configurations.shadow]
    relocate("org.apache.commons.pool2", "io.github.lxgaming.redisvelocity.lib.commonspool2")
    relocate("redis.clients.jedis", "io.github.lxgaming.redisvelocity.lib.jedis")
    relocate("redis.clients.util", "io.github.lxgaming.redisvelocity.lib.jedisutil")
}

jar {
    from {
        (configurations.compile - configurations.provided).findAll({
            it.isDirectory() || it.name.endsWith(".jar")
        }).collect({
            it.isDirectory() ? it : zipTree(it)
        })
    }
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-" + archivesBaseName)
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        if (project.hasProperty("maven.url")) {
            mavenDeployer {
                project.logger.lifecycle("Uploading Archives")
                repository(url: project.property("maven.url"))

                pom {
                    groupId = project.group
                    artifactId = project.archivesBaseName
                    version = project.version
                    project {
                        name = project.archivesBaseName
                        packaging = "jar"

                        scm {
                            url = "https://github.com/LXGaming/RedisVelocity"
                            connection = "scm:git:https://github.com/LXGaming/RedisVelocity.git"
                            developerConnection = "scm:git:git@github.com:LXGaming/RedisVelocity.git"
                        }

                        issueManagement {
                            system = "GitHub Issues"
                            url = "https://github.com/LXGaming/RedisVelocity/issues"
                        }

                        licenses {
                            license {
                                name = "Apache License 2.0"
                                url = "https://opensource.org/licenses/Apache-2.0"
                                distribution = "repo"
                            }
                        }
                    }
                }
            }
        }
    }
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        configurations.archives.allArtifacts.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}